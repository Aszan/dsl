apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'signing'

defaultTasks 'clean', 'compileJava', 'test'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.structurizr:structurizr-client:1.9.10'
    implementation 'com.structurizr:structurizr-adr-tools:1.3.8'

    testImplementation 'org.codehaus.groovy:groovy-jsr223:3.0.8'
    testImplementation 'org.jetbrains.kotlin:kotlin-scripting-jsr223:1.5.30'
    testImplementation 'org.jruby:jruby-core:9.2.19.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

sourceCompatibility = 1.8
targetCompatibility = 1.8

description = 'Structurizr DSL'
group = 'com.structurizr'
version = '1.16.1'

jacoco {
  toolVersion = "0.8.4"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
}

jar {
    manifest {
        attributes(
                'Implementation-Title':   description,
                'Implementation-Version': version
        )
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Structurizr DSL'
                packaging 'jar'
                description 'Structurizr DSL'
                url 'https://github.com/structurizr/dsl'

                scm {
                    connection 'scm:git:git://github.com/structurizr/dsl.git'
                    developerConnection 'scm:git:git@github.com:structurizr/dsl.git'
                    url 'https://github.com/structurizr/dsl'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id "simon"
                        name "Simon Brown"
                        email "simon@structurizr.com"
                    }
                }
            }
        }
    }
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "simon"
            name "Simon Brown"
            email "simon@structurizr.com"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Visualise, document and explore your software architecture with the C4 model.')
                root.appendNode('name', 'Structurizr DSL')
                root.appendNode('url', 'https://github.com/structurizr/dsl')
                root.children().last() + pomConfig
            }
        }
    }

    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}

